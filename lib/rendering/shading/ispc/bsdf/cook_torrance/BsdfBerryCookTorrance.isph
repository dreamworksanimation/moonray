// Copyright 2023-2024 DreamWorks Animation LLC
// SPDX-License-Identifier: Apache-2.0

/// @file BsdfBerryCookTorrance.isph
#pragma once
#include <moonray/rendering/shading/ispc/bsdf/Bsdf.isph>
#include <moonray/rendering/shading/ispc/bsdf/BsdfUtil.isph>
#include <moonray/rendering/shading/ispc/ShaderMacros.isph>

/// @class BerryCookTorranceBsdfLobe BsdfBerryCookTorrance.isph <rendering/shading/bsdf/BsdfBerryCookTorrance.isph>
/// @brief The Cook-Torrance bsdf using Berry Distribution.
///        WARNING: This Bsdf is not energy preserving. Do not use!
/// 
struct BerryCookTorranceBsdfLobe
{
    COOK_TORRANCE_LOBE_BASE;
};

/// @brief evaluate an berry cook torrance lobe
varying Color
BerryCookTorranceBsdfLobe_eval(const varying BsdfLobe * uniform lobe,
                               const varying BsdfSlice &slice,
                               const Vec3f &wi,
                               varying float * uniform pdf);

/// @brief sample an berry cook torrance lobe
varying Color
BerryCookTorranceBsdfLobe_sample(const varying BsdfLobe * uniform lobe,
                                 const varying BsdfSlice &slice,
                                 varying float r1, varying float r2,
                                 varying Vec3f &wi, varying float &pdf);

inline void
BerryCookTorranceBsdfLobe_show(const varying BsdfLobe * const uniform lobe,
                               const uniform uint8_t ntab)
{
    const varying BerryCookTorranceBsdfLobe * uniform me =
        (const varying BerryCookTorranceBsdfLobe * uniform) lobe;

    const varying Vec3f N = getN(me->mFrame);
    const varying Fresnel * const uniform fresnel = me->mFresnel;

    BsdfUtil_indent(ntab);   print("[BerryCookTorranceBsdfLobe] mask: %\n", lobe->mMask);

    BsdfUtil_indent(ntab+1); print("scale: % % %\n", BSDF_UTIL_EXTRACT(me->mScale.r),
                                                     BSDF_UTIL_EXTRACT(me->mScale.g),
                                                     BSDF_UTIL_EXTRACT(me->mScale.b));
    BsdfUtil_indent(ntab+1); print("N: % % %\n", BSDF_UTIL_EXTRACT(N.x),
                                                 BSDF_UTIL_EXTRACT(N.y),
                                                 BSDF_UTIL_EXTRACT(N.z));

    if (fresnel) {
        Fresnel_show(fresnel, ntab+1);
    }
}

/// @brief initialize an berry cook torrance lobe
inline void
BerryCookTorranceBsdfLobe_init(varying BerryCookTorranceBsdfLobe * uniform lobe,
                               const varying Vec3f &normal,
                               const varying float roughness)
{
    // TODO: Should use CookTorranceBsdfLobe_init() ?
    BsdfLobe_init((varying BsdfLobe * uniform) lobe,
                  /* name = */ BSDF_LOBE_BERRY_COOK_TORRANCE,
                  /* type = */ BSDF_LOBE_TYPE_REFLECTION | BSDF_LOBE_TYPE_GLOSSY,
                  /* differentialFlags = */ (uniform BsdfLobeDifferentialFlags)0,
                  /* isSpherical = */ false,
                  /* propertyFlags = */ BSDF_LOBE_PROPERTY_NORMAL | BSDF_LOBE_PROPERTY_ROUGHNESS,
                  /* evalFn = */ BerryCookTorranceBsdfLobe_eval,
                  /* sampleFn = */ BerryCookTorranceBsdfLobe_sample,
                  /* albedoFn = */ CookTorranceBsdfLobe_albedo,                 // Fallback to CookTorranceBsdfLobe.
                  /* differentialsFn = */ CookTorranceBsdfLobe_differentials,   // Fallback to CookTorranceBsdfLobe. 
                  /* getPropertyFn = */ CookTorranceBsdfLobe_getProperty,       // Fallback to CookTorranceBsdfLobe. 
                  /* showFn = */ BerryCookTorranceBsdfLobe_show);

    ReferenceFrame_init(lobe->mFrame, normal);

    lobe->mInputRoughness = roughness;

    // Apply roughness squaring to linearize roughness response
    // See "Physically-Based Shading at Disney" Siggraph course notes.
    lobe->mRoughness = lobe->mInputRoughness * lobe->mInputRoughness;

    // TODO: Smaller thresholds trigger nans
    lobe->mRoughness = max(0.001f, lobe->mRoughness);

    // Use a directional differential scale that varies with roughness
    lobe->mdDFactor = CookTorrance_sdDFactorMin + lobe->mRoughness * CookTorrance_sdDFactorSlope;
}

